include "global.tbh"

const AZURE_INIT_SIGNATURE=&h1FD3
const AZURE_TOPIC_METHOD="$iothub/methods/POST/"
const AZURE_TOPIC_PROPERTY_DESIRED="$iothub/twin/PATCH/properties/desired/"
const AZURE_TOPIC_PROPERTY_REPORTED="$iothub/twin/PATCH/properties/reported/"
const AZURE_TOPIC_TWIN="$iothub/twin/"
const AZURE_TOPIC_STREAMS="$iothub/streams/POST/"
const AZURE_TOPIC_RESULT="$iothub/twin/res/"
const AZURE_API_VERSION="?api-version=2019-03-31"
'const AZURE_MQTT_API_VERSION="?api-version=2018-06-30"
const AZURE_MQTT_API_VERSION="?api-version=2021-04-12"
const TOPIC_TIMESTAMP="iothub-creation-time-utc="
const AZURE_CONNECTION_TOUT=150

dim azure_state as en_azure_states=EN_AZURE_NOT_INITIALIZED
dim azure_rid as dword=0
dim device_bound_message_topic as string
dim server_bound_message_topic as string
dim azure_init_flag as word = 0
dim azure_dps_sock as byte = 255
dim azure_current_dps_server as string(64)=""
dim azure_assigned_hub as string(64)=""
dim azure_current_scope_id as string(16)
dim azure_current_device_id as string(32)
dim azure_current_primary_key as string(64)
dim azure_sas_token as string
dim azure_dps_authorization as string(64)
dim azure_current_json_key as string(64)
dim azure_current_json_value as string(64)
dim azure_http_buf as string
dim azure_assigned_status as boolean

dim azure_timer as byte = 0
dim azure_interface as pl_sock_interfaces
dim azure_telemetry_ts as string(36) = ""
declare azure_current_url as string
declare mqtt_sock_num as byte
declare mqtt_state as en_mqtt_states

declare function azure_generate_sas_token(byref resourceUri as string, byref signingKey as string, expiresInMins as word) as string
declare sub azure_debugprint(byref print_data as string)
declare sub azure_dps_device_provisioning()
declare sub azure_http_reset()
declare sub azure_mqtt_start()
declare sub azure_send_server_bound_msg(byref ts as string, byref json as string)

public sub azure_start(byref device_id as string, byref scope_id as string, byref dps_url as string, byref symmetricKey as string, interface as pl_sock_interfaces)
'	azure_init_flag=AZURE_INIT_SIGNATURE
	if device_id="" or scope_id="" or dps_url="" or symmetricKey="" then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() invalid parameters")
		#endif
		exit sub
	end if
		
	azure_current_device_id=device_id
	azure_current_scope_id=scope_id
	azure_current_dps_server=dps_url
	azure_current_primary_key=symmetricKey
	server_bound_message_topic="devices/"+azure_current_device_id+"/messages/events/"
	device_bound_message_topic="devices/"+azure_current_device_id+"/messages/devicebound/"
	azure_state=EN_AZURE_NOT_CONNECTED
	azure_telemetry_ts=""
	azure_init_flag=AZURE_INIT_SIGNATURE
	azure_timer=AZURE_CONNECTION_TOUT
	azure_interface=interface
	
end sub

public sub azure_on_connected()
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	
	mqtt_sub(device_bound_message_topic+"#",QOS_AT_LEAST_ONCE)
	mqtt_sub(AZURE_TOPIC_METHOD+"#",QOS_AT_LEAST_ONCE)
	mqtt_sub(AZURE_TOPIC_RESULT+"#",QOS_AT_LEAST_ONCE)
	mqtt_sub(AZURE_TOPIC_PROPERTY_DESIRED+"#",QOS_AT_LEAST_ONCE)
	mqtt_sub(AZURE_TOPIC_STREAMS+"#",QOS_AT_LEAST_ONCE)

	azure_rid = azure_rid+1
	mqtt_publish(AZURE_TOPIC_TWIN+"GET/?$rid="+str(azure_rid),"{}",QOS_AT_LEAST_ONCE)  
	
	#if AZURE_DEBUG_PRINT
		azure_debugprint("Server connected.")
	#endif
	azure_state=EN_AZURE_CONNECTED
	callback_azure_on_connected()
end sub

public sub azure_clear_credential()

	azure_current_scope_id = ""
	azure_current_device_id = ""
	azure_current_primary_key = ""
end sub

public sub azure_on_disconnected()
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	#if AZURE_DEBUG_PRINT
		azure_debugprint("Disconnected from server.")
	#endif
	
	azure_state=EN_AZURE_NOT_CONNECTED
	azure_telemetry_ts=""
	callback_azure_on_disconnected()
end sub

public sub azure_proc_mqtt_msg(byref topic as string,byref data as string, byref remaining_byte as word)
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	
	dim pos1, pos2, pos3 as byte=0
	dim item_name as string
	dim value as string
	dim s as string
	dim rid as string = 0
	dim c as byte=0
	
	'normal command
	if instr(1,topic,AZURE_TOPIC_METHOD,1)>0 then
		dim topic_len as byte=len(AZURE_TOPIC_METHOD)
		pos1=instr(topic_len+1,topic,"/",1)
		item_name=mid(topic,topic_len+1,pos1-topic_len-1)
		pos1=instr(topic_len+1,topic,"$rid=",1)
		rid=right(topic, len(topic)-pos1-4)
		callback_azure_command_triggered(item_name,data,rid)
		#if AZURE_DEBUG_PRINT
			azure_debugprint("Command: "+item_name+" Data: "+data)
		#endif				
		exit sub
	end if
	
	'offline command method-name=DEVMIG
	if instr(1,topic,device_bound_message_topic,1) then
		dim topic_len as byte=len(topic)
		pos1=instr(1,topic,"method-name",1)
		item_name=right(topic,topic_len-pos1-len("method-name"))
		callback_azure_command_triggered(item_name,data,rid)
		#if AZURE_DEBUG_PRINT
			azure_debugprint("Command: "+item_name+" Data: "+data)
		#endif				
		exit sub
	end if
	
	'device twin
	if instr(1, topic,AZURE_TOPIC_TWIN,1)>0 then
		dim ts as dword
		dim processing_desired as no_yes = YES
		
		if sys.runmode = PL_SYS_MODE_RELEASE then
			ts = sys.timercount32 + 10
		else
			ts = sys.timercount32 + 100000
		end if
		if instr(1, topic,"/res/400",1)>0 then
			exit sub
		end if
		
		if instr(1, topic,"/res/200",1)>0 or instr(1, topic, AZURE_TOPIC_PROPERTY_DESIRED, 1) >0 then
			
			pos1=instr(1,topic,"$rid=",1)
			if pos1>0 then
				rid=right(topic, len(topic)-pos1-4)
				azure_rid=lval(rid)
			end if
			
			do 
				if remaining_byte > 0 and (255 - len(data)) > remaining_byte then
					data = data + sock.tlsgetdata(remaining_byte)
					remaining_byte = 0
				end if

				pos1 = instr(1,data,"\x22",1)
				pos2 = instr(pos1+1,data,"\x22",1)
				if pos1=0 or pos2=0 then
					exit do
				end if
				
				item_name=mid(data,pos1+1,pos2-pos1-1)
				
				select case item_name
				case "desired":
					processing_desired = YES
					data = right(data,len(data)-11)
			
				case "reported":
					processing_desired = NO
					data = right(data,len(data)-12)
					
				case "$version":
					dim processed_bytes as byte
					
					value = azure_get_json_value(data, item_name)
					callback_azure_update_twin_version(value)
					processed_bytes = pos2+len(value)+2
					data = right(data,len(data)-processed_bytes)
					
				case else:
					dim processed_bytes as byte
					dim local_value as string
					dim stg_result as en_stg_status_codes
					
					stg_result = stg_sg(item_name,0,local_value,EN_STG_GET)
					value = azure_get_json_value(data, item_name)
					processed_bytes = pos2+len(value)+2
					'check if value is a string type, revmove 2 more bytes if so.
					if mid(data,pos2+2,1) = "\x22" then
						processed_bytes = processed_bytes+2
					end if
					data = right(data,len(data)-processed_bytes)
										
					if stg_result=EN_STG_STATUS_OK and value<>local_value then						
						if processing_desired = YES then
							#if AZURE_DEBUG_PRINT
								azure_debugprint("Remote value of \x22"+item_name+"\x22 was "+local_value+" changed to: "+value)
							#endif
							callback_azure_twin_changed(item_name, value)
									
							'Double confirm setting is set correctly here						
							value="{\x22"+item_name+"\x22:\x22"+value+"\x22}"						
							azure_rid=azure_rid+1
							mqtt_publish(AZURE_TOPIC_PROPERTY_REPORTED+"?$rid="+str(azure_rid),value,QOS_AT_MOST_ONCE)
						else
							
							#if AZURE_DEBUG_PRINT
								azure_debugprint("Report updated local value of \x22"+item_name+"\x22 change from " + value + " to " +local_value)
							#endif
							
							callback_stg_eeprom_set(item_name,0,"")
						end if
					end if				
				end select		
			loop while pos1>0 and pos2>0 and sys.timercount32 < ts
		end if
	end if
end sub

public sub azure_command_respond(byref command as string, byref returns as string, rid as string, result as en_azure_command_codes)
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	dim topic as string
	if returns="" then
		returns="{}"
	end if
	topic="$iothub/methods/res/"+str(result)+"/?$rid="+rid
	mqtt_publish(topic,returns,QOS_AT_MOST_ONCE)
	
end sub

public sub azure_proc_timer()
	select case azure_state
	case EN_AZURE_NOT_INITIALIZED:
		exit sub
	case EN_AZURE_NOT_CONNECTED:
		callback_azure_get_assigned_hub(azure_assigned_hub)
		if azure_assigned_hub <> "" and get_timestamp(EN_TS_RTC_UPDATED) > 0 then
			azure_mqtt_start()
			azure_state = EN_AZURE_DEVICE_LOOKUP
		else
			azure_device_lookup()
		end if
		
	case EN_AZURE_DEVICE_LOOKUP:
		if azure_timer >0 then
			azure_timer=azure_timer-1
		else
			azure_timer=AZURE_CONNECTION_TOUT
			callback_azure_conn_timeout()
		end if
		
	case EN_AZURE_NOT_PROVISIONED:
		azure_dps_device_provisioning()
		
	case EN_AZURE_PROVISIONING:
		if azure_timer >0 then
			azure_timer=azure_timer-1
		else
			azure_timer=AZURE_CONNECTION_TOUT
			callback_azure_conn_timeout()
		end if
	case EN_AZURE_CONNECTED:		
	end select	
end sub	

public function azure_update_json(byref original as string, byref attribute as string, byref value as string) as string
	if len(original) + len(attribute) + len(value) > 255 then
		exit function
	end if
	
	if original="" then
		original="{}"
	end if
	
	while right(original,1)<>"}"
		original=left(original,len(original)-1)
	wend
		
	if original="{}" then
		if left(value,1)="{" and right(value,1)="}" then
			azure_update_json="{\x22"+attribute+"\x22:"+value+"}"
		else
			azure_update_json="{\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	else
		if left(value,1)="{" and right(value,1)="}" then
			azure_update_json=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:"+value+"}"
		else
			azure_update_json=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	end if
end function

sub azure_request_property()
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	azure_rid=azure_rid+1
	mqtt_publish(AZURE_TOPIC_TWIN+"GET/?$rid="+str(azure_rid),"{}",QOS_AT_MOST_ONCE)   	
end sub

sub azure_send_property(prop_name as string, prop_value as string)
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	dim json as string
	json = azure_update_json(json,prop_name,prop_value)
	azure_rid=azure_rid+1
	mqtt_publish(AZURE_TOPIC_PROPERTY_REPORTED + "?$rid="+str(azure_rid),json,QOS_AT_MOST_ONCE)
end sub

public sub azure_send_telemetry()
	if mqtt_state<>EN_MQTT_STATES_LOGGED_IN or azure_telemetry_ts<>"" then
		exit sub
	end if
	
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if
	dim ts as string(36)
	dim same_ts as boolean = true
	dim json as string
	dim rec_num as word=1
	
	while same_ts = true
		tbl_select("Azure","Azure")
		if tbl_record_read_active(rec_num)=EN_TBL_STATUS_OK then
			dim record_type, record_value as string(64)
			tbl_field_sg("time",ts,EN_TBL_GET)
			tbl_field_sg("type", record_type,EN_TBL_GET)
			tbl_field_sg("value",record_value ,EN_TBL_GET)	
			json = azure_update_json(json,record_type,record_value)
			
			dim next_ts as string
			rec_num=rec_num+1
			if tbl_record_read_active(rec_num)=EN_TBL_STATUS_OK then
				tbl_field_sg("time",next_ts,EN_TBL_GET)
				if ts <> next_ts or (left(next_ts,4) = "2000" and record_type= "event") then
					same_ts = false
				end if
			else
				same_ts = false
			end if
		else
			exit sub
		end if
	wend
	azure_telemetry_ts=ts
	
	azure_send_server_bound_msg(ts, json)	
end sub

sub azure_send_server_bound_msg(byref ts as string, byref json as string)
	dim pos as byte
	if ts<>"" then
		pos=instr(1,server_bound_message_topic,TOPIC_TIMESTAMP,1)
		if pos>0 then
			server_bound_message_topic=left(server_bound_message_topic,pos-1)+TOPIC_TIMESTAMP+ts
		else
			server_bound_message_topic=server_bound_message_topic+TOPIC_TIMESTAMP+ts
		end if
	end if
	
	sock.num=mqtt_sock_num
	sock.notifysent(0)
	mqtt_publish(server_bound_message_topic,json,QOS_AT_MOST_ONCE)				
end sub


sub azure_set_datetime(byref dt_string as string)	
	'Mon, 08 Feb 2021 03:16:19 GMT
	dim daycounts, mincounts as word
	dim curr_daycounts, curr_mincounts as word
	dim syear,smonth,sdate,shour,smin,ssec as string(4)
	dim b as byte
	
	syear=mid(dt_string,15,2)
	smonth=mid(dt_string,9,3)
	sdate=mid(dt_string,6,2)
	shour=mid(dt_string,18,2)
	smin=mid(dt_string,21,2)
	ssec=mid(dt_string,24,2)
	
	select case smonth
	case "Jan": smonth="01"
	case "Feb": smonth="02"
	case "Mar": smonth="03"
	case "Apr": smonth="04"
	case "May": smonth="05"
	case "Jun": smonth="06"
	case "Jul": smonth="07"
	case "Aug": smonth="08"
	case "Sep": smonth="09"
	case "Oct": smonth="10"
	case "Nov": smonth="11"
	case "Dec": smonth="12"
	case else:
	end select
	daycounts=daycount(val(syear),val(smonth),val(sdate))
	mincounts=mincount(val(shour),val(smin))
	
	rtc.getdata(curr_daycounts,curr_mincounts, b)
	if curr_daycounts<>daycounts or curr_mincounts<>mincounts or get_timestamp(EN_TS_RTC_UPDATED) = 0 then
		callback_azure_rtc_correction(curr_daycounts,curr_mincounts, b)
		b=val(ssec) 	
		rtc.setdata(daycounts,mincounts,b)
		set_timestamp(EN_TS_RTC_UPDATED,device_rtc_get_serialized(daycounts,mincounts,b))
	end if
		
end sub

public sub azure_on_telemetry_sent()
	if sock.num<>mqtt_sock_num then
		exit sub
	end if
	
	if mqtt_state=EN_MQTT_STATES_LOGGED_IN then
		dim rec_num as word=1
	    tbl_select("Azure","Azure")
		while tbl_record_find(EN_TBL_RECORD_ACTIVE,azure_telemetry_ts,"time",rec_num,EN_TBL_SEARCH_DOWN,PL_FD_FIND_EQUAL) = EN_TBL_STATUS_OK
			tbl_record_delete(rec_num)
			rec_num=1
		wend
		azure_telemetry_ts=""
		callback_azure_on_telemetry_sent()	
	end if
end sub

sub azure_device_lookup()
	if azure_init_flag<>AZURE_INIT_SIGNATURE then
		#if AZURE_DEBUG_PRINT
			azure_debugprint("ERROR: azure_start() wasn't used or failed")
		#endif
		exit sub
	end if

	azure_dps_authorization=azure_generate_sas_token(azure_current_scope_id+"%2Fregistrations%2F"+azure_current_device_id, azure_current_primary_key, 525600)
	azure_current_url = "https://" + azure_current_dps_server  + "/" + azure_current_scope_id + "/registrations/"+azure_current_device_id+AZURE_API_VERSION
	azure_http_reset()
	
	http_request(HTTP_POST, azure_current_url, azure_interface,"{registrationId: \x22"+azure_current_device_id+"\x22}")
	azure_state=EN_AZURE_DEVICE_LOOKUP	

end sub

sub azure_dps_device_provisioning()
	if azure_state<>EN_AZURE_PROVISIONING then
		azure_state=EN_AZURE_PROVISIONING
		azure_dps_authorization=azure_generate_sas_token(azure_current_scope_id+"%2Fregistrations%2F"+azure_current_device_id, azure_current_primary_key, 525600)		
		
		azure_current_url = "https://" + azure_current_dps_server + "/" + azure_current_scope_id +"/registrations/"+azure_current_device_id+"/register"+AZURE_API_VERSION
		azure_http_reset()
		http_request(HTTP_PUT, azure_current_url, azure_interface,"{registrationId: \x22"+azure_current_device_id+"\x22}")			
	end if
end sub

sub iot_send_headers()
  if http_request_item.url = azure_current_url then
        http_send_headers("Content-Type","application/json")
		http_send_headers("Authorization", "SharedAccessSignature sr="+azure_current_scope_id+"%2Fregistrations%2F"+azure_current_device_id+"&sig="+azure_dps_authorization)
    end if
end sub

sub azure_mqtt_start()
	dim user_name as string=azure_assigned_hub+"/"+azure_current_device_id+"/"+AZURE_MQTT_API_VERSION	
	dim resourceUri as string= azure_assigned_hub+"%2Fdevices%2F"+azure_current_device_id
	azure_sas_token="SharedAccessSignature sr="+azure_assigned_hub+"%2Fdevices%2F"+azure_current_device_id+"&sig="+azure_generate_sas_token(resourceUri, azure_current_primary_key, 525600)
	
	mqtt_start(azure_interface,azure_assigned_hub,8883,user_name,azure_sas_token,azure_current_device_id,true)
end sub

sub azure_json_callback(byref key as string, byref value as string)
	select case azure_state
	case EN_AZURE_DEVICE_LOOKUP:
		if key = "assignedHub" then
			azure_assigned_hub=value
			callback_azure_device_assigned(value)
		end if
		if key = "status" and value = "assigned" then
			azure_assigned_status=true
		end if
		if key = "errorCode" then
			azure_assigned_status=false
		end if

	case EN_AZURE_PROVISIONING:
		if key = "status" and value = "assigned" then
			azure_assigned_status=true
		end if
		if key = "errorCode" then
			callback_azure_provision_failed()
			azure_assigned_status=false
		end if
	end select
	#if AZURE_DEBUG_PRINT
		azure_debugprint(key+":"+value)
	#endif
end sub

function azure_parse_http(data as string) as string
	azure_parse_http = ""
	dim start_pos as byte = 0
	dim end_pos as byte = 0
	dim segment_length as byte

	do
		segment_length = 255 - len(azure_http_buf)
		if segment_length > len(data) then
			segment_length = len(data)
		end if
		azure_http_buf = azure_http_buf + mid(data,1,segment_length)
		data = right(data,len(data) - segment_length)
		
		start_pos = instr(1,azure_http_buf,"{",1)
		if start_pos = 0 then start_pos = instr(1,azure_http_buf,",",1)
		end_pos = instr(start_pos+1,azure_http_buf,",",1)
		if end_pos = 0 then end_pos = instr(start_pos+1,azure_http_buf,"}",1)
		
		if start_pos> 0 and end_pos > 0 then
			dim item as string = mid(azure_http_buf, start_pos+1, end_pos - start_pos - 1)
			dim sep_pos as byte = instr(1,item,":",1)
			if sep_pos > 0 then
				azure_current_json_key = mid(item, 1, sep_pos - 1)
				azure_current_json_value = mid(item, sep_pos+1, len(item) - sep_pos)
				if right(azure_current_json_key,1) = "\x22" then azure_current_json_key = left(azure_current_json_key,len(azure_current_json_key)-1)
				if left(azure_current_json_key,1) = "\x22" then azure_current_json_key = right(azure_current_json_key,len(azure_current_json_key)-1)
				if right(azure_current_json_value,1) = "\x22" then azure_current_json_value = left(azure_current_json_value,len(azure_current_json_value)-1)
				if left(azure_current_json_value,1) = "\x22" then azure_current_json_value = right(azure_current_json_value,len(azure_current_json_value)-1)				
				azure_json_callback(azure_current_json_key, azure_current_json_value)
				azure_current_json_key = ""
				azure_current_json_value = ""
			end if
			azure_http_buf = right(azure_http_buf,len(azure_http_buf) - end_pos + 1)	
		end if
	loop while start_pos > 0 and end_pos > 0
	
end function

sub iot_header_arrival(header_type as string, header_value as string)
	if header_type = "Date" then
		azure_set_datetime(header_value)
	end if
end sub

sub azure_http_request_failed()
	'if request to dps server failed, but we have previously stored hub, try connect to hub without dps look up
	if http_request_item.url = azure_current_url and azure_state = EN_AZURE_DEVICE_LOOKUP then
		if azure_assigned_status=false then
			azure_state=EN_AZURE_NOT_PROVISIONED
		end if
		'if device is already registered, skip provisioning
		callback_azure_get_assigned_hub(azure_assigned_hub)
		if azure_assigned_hub<>"" and azure_assigned_status<>false then
			azure_mqtt_start()
		end if	
	end if		
end sub

sub azure_http_request_complete()
	if http_request_item.url = azure_current_url then
		select case azure_state		
		case EN_AZURE_DEVICE_LOOKUP: 
			if azure_assigned_status=false then
				azure_state=EN_AZURE_NOT_PROVISIONED
			end if
			'if device is already registered, skip provisioning
			callback_azure_get_assigned_hub(azure_assigned_hub)
			if azure_assigned_hub<>"" and azure_assigned_status<>false then
				azure_mqtt_start()
			end if	
		case EN_AZURE_PROVISIONING: 
			'after provisioning, lookup the device
			callback_azure_on_provisioning()
			azure_state=EN_AZURE_NOT_CONNECTED
		end select
	end if
end sub

sub azure_content_arrival(data as string)
	if http_request_item.url = azure_current_url then
		azure_parse_http(data)
	end if
end sub

sub azure_http_reset()
	azure_http_buf = ""
	azure_current_json_key = ""
	azure_current_json_value = ""
	azure_assigned_status = false
end sub

function azure_generate_sas_token(byref resourceUri as string, byref signingKey as string, expiresInMins as word) as string

	dim key as string="key"
	dim message as string
	dim result as string=strgen(255,"A")
	dim ch as string(1)
	dim s as string
	dim daycounts,mincounts as word
	dim seconds as byte
	dim expires as dword
	dim i as byte
	
	rtc.getdata(daycounts,mincounts,seconds) 'rtc.getdata returns the elapse time since 2000, we need the elapse time since 1970

	expires=daycounts*24*60+mincounts+16302369+expiresInMins
	expires=expires*60+seconds
	
	message=resourceUri+"\x0A"+lstr(expires)
	base64_decode(signingKey,key)
	sock.tlsinit(0)
	hmac256(key,message,result)
	sock.tlsdeinit()
	
	for i=0 to len(result)/2-1
		ch=chr(val("&h"+mid(result,i*2+1,2)))
		s=s+ch
	next i
	base64_encode(s,len(s),result)
	
	azure_generate_sas_token=""
	for i=1 to len(result)
		ch=mid(result,i,1)
		if ch="/" then
			azure_generate_sas_token=azure_generate_sas_token+"%2F"
		else
			if ch="=" then
				azure_generate_sas_token=azure_generate_sas_token+"%3D"
			else
				if ch="+" then
					azure_generate_sas_token=azure_generate_sas_token+"%2B"
				else
					azure_generate_sas_token=azure_generate_sas_token+ch
				end if
			end if
		end if
	next i
	azure_generate_sas_token=azure_generate_sas_token+"&se="+lstr(expires)

end function

function azure_get_json_value(byref json_str as string, byref attribute as string) as string
	dim pos1, pos2, pos3 as byte
	dim len1 as byte
	dim start_chr as string(1)
	
	azure_get_json_value = ""
	pos1 = instr(1,json_str,attribute,1)
	if pos1 = 0 then exit function
	
	pos1 = instr(pos1+len(attribute),json_str,":",1)
	if pos1 = 0 then exit function
	pos1 = pos1 + 1
	
	start_chr = mid(json_str,pos1,1)
	
	select case start_chr
	'if attribute value start with {, assume it's a json object, has to end with }
	case "{":
		pos2 = pos1
		pos3 = pos1
		do	
			pos2 = instr(pos2+1,json_str,"}",1)
			pos3 = instr(pos3+1, json_str,"{",1)
		loop while pos3 > 0 and pos3 < pos2
		pos2 = pos2 + 1	
	
	'if attribute value starts with ", assume it's a string, has to end with "
	case "\x22":
		pos2 = pos1
		do 
			pos3 = instr(pos2+1,json_str,"\x5c",1)
			pos2 = instr(pos2+1,json_str,"\x22",1)
		loop while pos2 > 0 and pos3 = pos2 - 1
		pos2 = pos2 + 1
		
	case else:
		pos2 = instr(pos1,json_str,",",1)
		pos3 = instr(pos1,json_str,"}",1)
		if (pos3>0 and pos3<pos2) or pos2 = 0 then
			pos2 = pos3 
		end if	
	end select
	
	if pos2 = 0 then exit function
	
	azure_get_json_value = mid(json_str,pos1,pos2-pos1)
	
	if left(azure_get_json_value,1) = "\x22" then
		azure_get_json_value = mid(azure_get_json_value,2,len(azure_get_json_value)-2)
	end if	
end function

#if AZURE_DEBUG_PRINT
	sub azure_debugprint(byref print_data as string)
		dim s as string	
		s = "AZURE> "+print_data+"\x0d\x0a"
		callback_azure_debugprint(s)
		#if AZURE_DEBUG_PRINT = 2
			sys.debugprint(s)
		#endif

	end sub
#endif	


