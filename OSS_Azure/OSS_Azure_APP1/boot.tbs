include "global.tbh"

'CONSTANTS-----------------------------------------------------------

const CELL__SIMCARD_PIN = CELL__QUESTMARK	'<-- CHANGE THIS AS NEEDED

dim DNS_socket as byte
dim comm_mode as byte '0: WIFI, 1: LTE
dim connect_to_azure_required as no_yes=NO
dim min_rec_num_to_send as word
dim config_mode as no_yes=NO
dim config_bt_name as string(16) = "BT-OSS-APP1"
dim CELL__AT_APN as string(24) = "internet.iot"			'<-- CHANGE THIS AS NEEDED
dim CELL__MYUSERNAME as string(16)  = "myusername@realm"	'<-- CHANGE THIS AS NEEDED
dim CELL__MYPASSWORD as string(16)  = ""					'<-- CHANGE THIS AS NEEDED
dim sensor_type as byte
dim operation_mode as byte '0: always on, 1: low power mode

declare sub check_app_fw_version()
declare sub sensor_init()
declare sub pic_init()
declare sub check_and_upgrade_supr_pic()
declare sub check_and_upgrade_sns_pic()

declare function turn_sensor3v3_on() as boolean
declare function turn_sensor3v3_off() as boolean
declare modbus_device_id as byte
declare command_issued as no_yes

sub boot()
    'sys.debugmode = PL_SYS_DBG_SERIAL
	'sys.debugmode = PL_SYS_DBG_NET
	
    'Fd(flash disk) setup
    '================================================================
    if fd.mount<>PL_FD_STATUS_OK then
        if fd.formatj(fd.availableflashspace,32,100)<>PL_FD_STATUS_OK then
            if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
        end if
    
        if fd.mount<>PL_FD_STATUS_OK then
            if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
        end if
    end if

    'Tables setup
    '================================================================
    if tbl_start()<>EN_TBL_STATUS_OK then
        if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
    end if
	
    'Setting Library Initialization	
    '================================================================
    dim stg_init_code as en_stg_status_codes
    dim stg_name as string(STG_MAX_SETTING_NAME_LEN)
	
	stg_init_code = stg_start_no_init()
    if stg_init_code<>EN_STG_STATUS_OK then 
        pat.play("R-R-~",PL_PAT_CANINT)
        if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
	else
		check_app_fw_version()
		stg_init_code=stg_check_all(stg_name)
		select case stg_init_code
		case EN_STG_STATUS_OK:
		'--- all good ---
		case EN_STG_STATUS_INVALID, EN_STG_STATUS_FAILURE:
			if stg_restore_multiple(EN_STG_INIT_MODE_NORMAL)<>EN_STG_STATUS_OK then
				pat.play("R-R-~",PL_PAT_CANINT)
				if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
			end if
		case else:
			'some other trouble
			pat.play("R-R-~",PL_PAT_CANINT)
			if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
		end select	
    end if
	
	if stg_init_code<>EN_STG_STATUS_OK then
		device_debug_print("Setting init failed.")
	end if

	CELL__AT_APN = stg_get("GAN",0)
	CELL__MYUSERNAME = stg_get("GUR",0)
	CELL__MYPASSWORD = stg_get("GPW",0)
	operation_mode = stg_get("OPMD",0)
    '-----------------------------------------------------------------
	
    dim f as byte
    for f=0 to MAX_NUM_INTERFACES-1
        interface_ready(f)=NO
    next f
	
    '-----------------------------------------------------------------
	'mcp23017 setup on socket 5
	'================================================================
	select case mcp23017_init("TBT00_3_S5", YES, TBT00_3_S5_CLK, TBT00_3_S5_DATA, TBT00_3_S5_CHANNEL)
	case MCP23017_SELF_TEST_FAIL:
		sys.debugprint("mcp23017 self test failed!\r\n")
		if sys.runmode = PL_SYS_MODE_DEBUG then sys.halt
		pat.play("R-R-R",PL_PAT_CANINT)
		device_debug_print("mcp2307 init failed.")
	case MCP23017_NO_SSI_AVAILABLE:
		sys.debugprint("SSI channel could not be allocated, using bit banging instead.\r\n")
	end select
	
	pic_init()
	check_and_upgrade_supr_pic()
	
	mcp23017_line_out_enable(VMOD_SHDN_LINENUM,VMOD_SHDN_PORT,YES,TBT00_3_S5_CHANNEL)
	mcp23017_line_set(VMOD_SHDN_LINENUM,VMOD_SHDN_PORT,LOW,TBT00_3_S5_CHANNEL)

	'allow pic to put device into sleep
	mcp23017_line_out_enable(PMU_INHIBIT_LINE,PMU_INHIBIT_PORT,yes,TBT00_3_S5_CHANNEL)
	mcp23017_line_set(PMU_INHIBIT_LINE,PMU_INHIBIT_PORT,HIGH,TBT00_3_S5_CHANNEL)
	
	read_ntag()
	turn_sensor3v3_on()
	'-----------------------------------------------------------------	
	
	dim bl as string(1) = pic_read(&h02,1)
	battery_level = asc(bl)
	if battery_level > 100 then battery_level=0

	sensor_init()	
	
	dim scid as string(2) = pic_read(&h03,2)
	dim msb as word = asc(left(scid,1))
	dim lsb as word = asc(right(scid,1))
	dim cid as word = msb*256+lsb
	
	stg_set("CID",0,str(cid))
	
	if config_mode = YES then
		'Bluetooth setup
		'================================================================
		bt.name=config_bt_name
		bt.emulation=PL_WLN_BT_EMULATION_MODE_MICROCHIP
		bt.txbuffrq(1)
		bt.rxbuffrq(1)
		sys.buffalloc()
		'enable the BLE interface and wait for it to become enabled
		'(blocking version)
		bt.enable
		while bt.enabled=NO
		wend
		bt.advertise=YES 'allow the device to be discoverable
		'-----------------------------------------------------------------
		luis_start(config_bt_name)
		luis_bt_enabled = true
		device_awake_timer=1200
		exit sub
	end if
	
	upgrade_app_fw_init()
	connect_to_azure_required = need_to_connect()
	'-----------------------------------------------------------------
	
    'WM2000 Wi-Fi setup
    '================================================================               
	'stg_set("MODE",0,"1")
	comm_mode=val(stg_get("MODE",0))
	
	if connect_to_azure_required=YES then
		'if sock debugprint is on, autoconnect no matter what comm mode is
		#if SYS_SOCK_DEBUGPRINT

			if wln.autoconnect<>YES or wln.autodhcp<>YES then	
				wln.autodhcp=YES
				wln.autoconnect=YES
				sys.reboot 
			end if		
		
			sock.num=SYS_DPRINT_SOCK_NUM
			sock.txbuffrq(5)
			sys.buffalloc()
			sock.protocol=PL_SOCK_PROTOCOL_TCP
			sock.targetip=SYS_DPRINT_TARGET_IP  'changes as see fit
			sock.targetport=SYS_DPRINT_TARGET_PORT
			
		#else
			if comm_mode = 0 and wln.autoconnect = NO then
				wln.autoconnect = YES
				sys.reboot()
			end if
			
			if comm_mode = 1 and wln.autoconnect = YES and (sys.runmode =PL_SYS_MODE_RELEASE or sys.debugmode = PL_SYS_DBG_SERIAL) then
				wln.autoconnect = NO
				sys.reboot()
			end if		
		#endif	
	
		'DNS setup
		'================================================================
		DNS_socket=sock_get("DNS")
		dns_start(DNS_socket)
		'-----------------------------------------------------------------
	
		http_start()

		if comm_mode=0 then
			current_interface = PL_SOCK_INTERFACE_WLN
			device_awake_timer = AWKE_TIME_FOR_WLN*120
		else
			current_interface = PL_SOCK_INTERFACE_PPP
			device_awake_timer = AWKE_TIME_FOR_CELL*120
			cell__start(CELL__SIMCARD_PIN, CELL__AT_APN, CELL__MYUSERNAME, CELL__MYPASSWORD)
		end if
	end if
	
	if upgrade_state <> 255 then
		device_awake_timer = device_awake_timer*2
	end if
	sleep_time=val(stg_get("SPT",0))

'	if sys.resettype=PL_SYS_RESET_TYPE_EXTERNAL then
'		device_add_event("reset due to power loss")		
'	end if
    pat.play("B-B-B-",PL_PAT_CANINT)
	
	if pic_read(&hA3,1) = "\x00" and stg_get("NWPR",0) = "1" then
		device_add_event("Password Recovery Success.")
		stg_set("NWPR",0,"0")
	end if
	
	if stg_get("CMD",0)<>"" then
		command_issued = YES
	end if
end sub

sub check_app_fw_version()
	dim s as string
	s=stg_get("FWV",0)
	if OSS_APP_FW_VER<>s then
		dim pos1 as byte = instr(1,s,".",1)
		dim pos2 as byte = instr(1,s,".",2)
		dim w1 as word = val(mid(s,2,pos1-2))*100+val(mid(s,pos1+1,pos2-pos1-1))
		
		if w1<202 then
			stg_set("CMD",0,"")
			stg_set("MNUM",0,"")
			goto fw203
		else if w1>=203 and w1<204 then
fw203:		
		end if
		stg_set("FWV",0,OSS_APP_FW_VER)
		device_add_event("Upgrage done! "+OSS_APP_FW_VER)		
	end if
end sub

sub sensor_level_shift_on()
	io.num=SENSOR_CONTROL_PIN
    io.enabled = YES
    io.state = HIGH
end sub

sub alternate_debugprint(byref s as string)
	#if SYS_EVENT_DEBUGPRINT
		device_add_event(s)
	#endif
	#if SYS_SOCK_DEBUGPRINT
		dim b as byte=sock.num
		sock.num=SYS_DPRINT_SOCK_NUM
		sock.setdata(s)
		sock.send()
		sock.num=b
	#endif
end sub

sub init_i2c_sensor()

	sensor_channel = si2c_register("SNR", SENSOR_DAT_PIN, SENSOR_CLK_PIN, NO)
	if sensor_channel<4 then
        ssi.channel=sensor_channel
        ssi.enabled=NO
        ssi.baudrate=PL_SSI_BAUD_100kHz

        io.num=SENSOR_DAT_PIN
        io.state=HIGH
        io.num=SENSOR_CLK_PIN
        io.state=HIGH
        ssi.zmode = PL_SSI_ZMODE_ENABLED_ON_ZERO

        ssi.direction=PL_SSI_DIRECTION_LEFT
        ssi.domap = SENSOR_DAT_PIN
        ssi.dimap = SENSOR_DAT_PIN
        ssi.clkmap = SENSOR_CLK_PIN
        ssi.mode = PL_SSI_MODE_0

        ssi.enabled=YES
    end if
	delay_ms(100)
end sub

sub init_modbus_sensor()
	'enable modbus voltage
	mcp23017_line_set(VMOD_SHDN_LINENUM,VMOD_SHDN_PORT,HIGH,TBT00_3_S5_CHANNEL)

	sensor_level_shift_on()

	'Modbus RS485 setup on UART 1
    '================================================================
	ser.num = MODBUS_UART
    ser.enabled = NO

	io.num = MODBUS_RTS
    io.enabled = YES
    io.state = LOW
	ser.rtsmap = MODBUS_RTS
	ser.dircontrol = PL_SER_DCP_LOWFORINPUT

    ser.bits=PL_SER_BB_8
    ser.flowcontrol=PL_SER_FC_DISABLED
	select case sensor_type
	
	case 128:
		ser.baudrate=PL_SER_BAUD_38400 '38400 for the BP02
	case 129:
		ser.baudrate=PL_SER_BAUD_9600 '38400 for the BP02
	end select
   
    ser.interface=PL_SER_SI_HALFDUPLEX
    ser.esctype=PL_SER_ET_DISABLED
    ser.interchardelay=0
    ser.txbuffrq(1)
    ser.rxbuffrq(1)
    sys.buffalloc()
    ser.enabled = YES

    'Modbus Setup
    '==================================
    modbus_initialize()
    'set these to the interface number you want to use
	
	modbus_device_id = val(stg_get("MBID",0))
end sub

sub sensor_init()
	sensor_type = val(stg_get("STPY",0))
	select case sensor_type
	
	case 0, 1:	'temp_hum / temp_hum_co2
		if sensor_type = 0 then sample_timer = 4
		if sensor_type = 1 then sample_timer = 10
		init_i2c_sensor()
		check_and_upgrade_sns_pic()
		
	case 128,129: 'modbus sensor
		sample_timer = 4
		init_modbus_sensor()
		
	case else:
		exit sub
	end select
end sub

sub read_ntag()
	dim ntag as string(3)
	dim command as string(1)
	dim parameter as string(2)
	
	ntag=pic_read(&h90,3)	
	command = left(ntag,1)
	parameter = right(ntag,2)
	
	select case command
	case "\xB6","\xB3":
		if parameter <> "\x00\x00" then
			config_mode = YES
			config_bt_name=parameter+"-"+stg_get("DVNM",0)
		end if
	case else:		
	end select
end sub

sub enter_LVP_mode()
	io.num=PL_IO_NUM_1_TX0
	io.enabled=YES
	io.state=HIGH

	'set LVP mode
	mcp23017_line_out_enable(LVP_ENABLE_LINE, LVP_ENABLE_PORT, YES, TBT00_3_S5_CHANNEL)
	mcp23017_line_set(LVP_ENABLE_LINE, LVP_ENABLE_PORT, LOW, TBT00_3_S5_CHANNEL)

	'keep ACSIP alive
	mcp23017_line_out_enable(PMU_INHIBIT_LINE, PMU_INHIBIT_PORT, YES, TBT00_3_S5_CHANNEL)
	mcp23017_line_set(PMU_INHIBIT_LINE, PMU_INHIBIT_PORT, LOW, TBT00_3_S5_CHANNEL)

	io.num=PL_IO_NUM_1_TX0
	io.state=LOW
	io.state=HIGH	
	
	pic_lvp_start(PL_IO_NUM_9, PL_IO_NUM_4, PL_IO_NUM_1_TX0)
	
end sub

sub exit_LVP_mode()
	io.num=PL_IO_NUM_1_TX0
	io.enabled=YES
	io.state=HIGH
	
	delay_ms(100)
	
	'set LVP mode
	mcp23017_line_out_enable(LVP_ENABLE_LINE, LVP_ENABLE_PORT, YES, TBT00_3_S5_CHANNEL)
	mcp23017_line_set(LVP_ENABLE_LINE, LVP_ENABLE_PORT, HIGH, TBT00_3_S5_CHANNEL)

	'keep ACSIP alive
	mcp23017_line_out_enable(PMU_INHIBIT_LINE, PMU_INHIBIT_PORT, YES, TBT00_3_S5_CHANNEL)
	mcp23017_line_set(PMU_INHIBIT_LINE, PMU_INHIBIT_PORT, HIGH, TBT00_3_S5_CHANNEL)
	
end sub

sub check_and_upgrade_sns_pic()
	dim ver as string(3) = ""
	
	sensor_level_shift_on()
	si2c_get(sensor_channel)
	' Read FW version
	si2c_start()
	si2c_write(SENSOR_ADDR or PIC_WR)
	si2c_write(&h03)
	si2c_start()
	si2c_write(SENSOR_ADDR or PIC_RD)
	
	ver = ver + chr(si2c_read(true))
	ver = ver + chr(si2c_read(true))
	ver = ver + chr(si2c_read(true))
	si2c_stop()
	device_debug_print("Current version: " + ver)
	
	if (ver <> OSS_SNS_FW_VER) then
		' FLASHING
		pic_lvp_start(SENSOR_CLK_PIN, SENSOR_DAT_PIN, SENSOR_CONTROL_PIN)
		if not pic_lvp_upload_firmware(OSS_SNS_PIC_FILE) then
			device_debug_print("SNS PIC FW upgraded failed!")
			if ver = "\xFF\xFF\xFF" then
				device_add_event("Sensor not connected.")
			end if
		else
			device_debug_print("SNS PIC FW upgraded!")
			sys.reboot()			
		end if
	end if
	
end sub

sub check_and_upgrade_supr_pic()	
	dim ver as string(3) = pic_read(&h98,3)
	dim upgrade_success as boolean
	
	if ver <> OSS_SUPR_FW_VER then
		device_debug_print("Running PIC: "+ver+ "/ Current PIC: "+OSS_SUPR_FW_VER)
		enter_LVP_mode()
		pic_write(&h30,"\x77")
		upgrade_success = pic_lvp_upload_firmware(OSS_SUPR_PIC_FILE)
		
		io.num=PL_IO_NUM_1_TX0
		io.state=HIGH
		delay_ms(100)
		
		' Set PIC to POST LVP mode 
		pic_write(&h30,"\x88")
		delay_ms(100)
		
		' RESET PIC
		io.num=PL_IO_NUM_1_TX0
		io.state=LOW
		exit_LVP_mode()
		
		if upgrade_success = false then
			device_debug_print("SUPR PIC FW upgraded failed!")
		else
			device_debug_print("SUPR PIC FW upgraded!")
			sys.reboot()
		end if
	end if
end sub

sub pic_init()
	
	pic_channel = si2c_register("PIC", PIC_DAT_PIN, PIC_CLK_PIN, YES)
	if pic_channel<4 then
		ssi.channel=pic_channel
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUD_100kHz

		io.num=PIC_DAT_PIN
		io.enabled = YES
		io.state=HIGH

		io.num=PIC_CLK_PIN
		io.enabled = YES
		io.state=HIGH

		ssi.zmode = PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.domap = PIC_DAT_PIN
		ssi.dimap = PIC_DAT_PIN
		ssi.clkmap = PIC_CLK_PIN
		ssi.mode = PL_SSI_MODE_0

		ssi.enabled=YES
	end if
end sub

